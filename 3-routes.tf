#
# internet gateway & route table
#

resource "aws_internet_gateway" "main" {
  vpc_id = aws_vpc.main.id

  tags = {
    Name = "Main Internet Gateway"
  }
}

resource "aws_route_table" "public_route_table" {
  vpc_id = aws_vpc.main.id

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.main.id
  }

  tags = {
    Name = "Public Route Table"
  }
}

resource "aws_route_table_association" "public_subnet" {
  route_table_id = aws_route_table.public_route_table.id

  count = length(var.public_subnet_cidrs)
  subnet_id = element(aws_subnet.public[*].id, count.index)
}

#
# NAT gateway & Elastic IP (EIP)
#

resource "aws_eip" "eip" {
  domain = "vpc"
}

resource "aws_nat_gateway" "nat" {
  allocation_id = aws_eip.eip.id
  subnet_id = aws_subnet.public[0].id

  tags = {
    Name = "NAT in Public subnet 0"
  }

  depends_on = [aws_internet_gateway.main]
}
